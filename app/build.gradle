import com.android.tools.profgen.ArtProfileKt
import com.android.tools.profgen.ArtProfileSerializer
import com.android.tools.profgen.DexFile

plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'org.jetbrains.kotlin.android'
}

def calcVersionCode() {
    def versionCodeFile = file('versionCode.txt')
    def versionCode = versionCodeFile.text.trim().toInteger()
    return versionCode + abiId.toInteger()
}

android {
    namespace 'io.github.saeeddev94.xray'
    compileSdk 34

    defaultConfig {
        applicationId 'io.github.saeeddev94.xray'
        minSdk 26
        targetSdk 34
        versionCode calcVersionCode()
        versionName '5.2.0'

        vectorDrawables {
            useSupportLibrary true
        }

        vectorDrawables.generatedDensities = []

        externalNativeBuild {
            ndkBuild {
                arguments 'LOCAL_LDFLAGS += -Wl,--build-id=none -Wl,--hash-style=gnu'
            }
        }
    }

    aaptOptions {
        cruncherEnabled = false
    }

    packagingOptions {
        doNotStrip '**/*.so'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    externalNativeBuild {
        ndkVersion = '26.1.10909125'
        ndkBuild {
            path = 'src/main/jni/Android.mk'
        }
    }

    splits {
        abi {
            enable true
            reset()
            //noinspection ChromeOsAbiSupport
            include abiTarget.split(',')
            universalApk false
        }
    }
}

project.afterEvaluate {
    tasks.compileReleaseArtProfile.doLast {
        outputs.files.each { file ->
            if (file.toString().endsWith(".profm")) {
                println("Sorting ${file} ...")
                def version = ArtProfileSerializer.valueOf("METADATA_0_0_2")
                def profile = ArtProfileKt.ArtProfile(file)
                def keys = new ArrayList(profile.profileData.keySet())
                def sortedData = new LinkedHashMap()
                Collections.sort keys, new DexFile.Companion()
                keys.each { key -> sortedData[key] = profile.profileData[key] }
                new FileOutputStream(file).with {
                    write(version.magicBytes$profgen)
                    write(version.versionBytes$profgen)
                    version.write$profgen(it, sortedData, "")
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar', '*.jar'])
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.22')
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    implementation 'androidx.room:room-runtime:2.6.1'
    ksp 'androidx.room:room-compiler:2.6.1'
    implementation 'com.google.android.material:material:1.11.0'
}
